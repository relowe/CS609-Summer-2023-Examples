Parser Grammar
==============
< Program >     ::= < Program > < Statement > 
                    | < Statement >

< Statement >   ::= < Identifier > < Statement' > NEWLINE
                    | < Var-Decl > NEWLINE
                    | < Print > NEWLINE
                    | < Expression > NEWLINE
                    | < While > NEWLINE
                    | < Branch > NEWLINE
                    | < Function-Def > NEWLINE

< Statement' >  ::= EQUAL < Expression > 
                    | < Expression' >

< Var-Decl >    ::= < Type > < Identifier >

< Print >       ::= PRINT < Expression >

< While >       ::= WHILE < Condition > NEWLINE < Block >

< Branch >      ::= IF < Condition > NEWLINE < Block >

< Condition>    ::= < Expression > EQUAL < Expression >
                    | < Expression > NOTEQUAL  < Expression >

< Block >       ::= < Block > < Statement > 
                    | < Statement > END

< Function-Def > ::= < Function-Head > NEWLINE < Block > 

< Function-Head >::= FUNCTION IDENTIFIER LPAREN < Parameter-List > RPAREN RETURNS < Return-Type >

< Return-Type >  ::= < Type > | VOIDT

< Parameter-List > ::= < Parameter-List > COMMA < Var-Decl >
                       | < Var-Decl >
                       | ""

< Expression >  ::= < Term > < Expression' >

< Expression' > ::= PLUS < Term  > < Expression' >
                    | MINUS < Term > < Expression' >
                    | ""
                    
< Term >        ::= < Factor > < Term' >

< Term' >       ::= TIMES  < Factor > < Term' >
                    | DIVIDE < Factor > < Term' >
                    | ""

< Factor >      ::= < Base > POW < Factor >
                    | < Base >

< Base >        ::= LPAREN < Expression > RPAREN
                    | MINUS < Expression > 
                    | < Number >

< Number >      ::= INTLIT
                    | REALLIT
                    | < Ref >

< Ref >         ::= IDENTIFIER < Ref' >

< Ref' >        ::= LPAREN < Arg-List > RPAREN
                    | ""

< Arg-List > ::= < Arg-List > COMMA < Expression >
                 | < Expression >
                 | ""

Lexer Grammar
=============
NEWLINE     \n
PLUS        +
MINUS       -
TIMES       *
DIVIDE      /
POW         ^
LPAREN      (
RPAREN      )
INTLIT      [0-9]+
REALLIT     INTLINT.INTLIT
PRINT       print
IDENTIFIER  [a-zA-Z_][a-zA-Z0-9_]*
INTEGER     integer
REAL        real
EQUAL       =
NOTEQUAL    !=
WHILE       while
IF          if
END         end
FUNCTION    function
RETURNS     returns
VOIDT       void
COMMA       ","
