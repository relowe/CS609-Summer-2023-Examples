< Program >     ::= < Program > < Statement > 
                    | < Statement >

< Statement >   ::= < Expression > "\n"
                    | < Var-Decl > "\n"
                    | < Record-Def >
                    | < Assignment > "\n"
                    | < Print > "\n"

< Var-Decl >    ::= < Type > < Identifier >
                    | < Type > "[" < Bounds > "]" < Identifier >

< Record-Def >  ::= "record" < Identifier > "\n" < Fields > "end" 

< Fields >      ::= < Fields > < Var-Decl > "\n"
                    | < Var-Decl > "\n

< Bounds >      ::= < Bounds > "," < Integer-Lit >
                    | < Integer-Lit >

< Assignment >  ::= < Ref > "=" < Expression >

< Type >        ::= "integer" | "real"

< Print >       ::= "print" < Expression >

< Expression >  ::= < Expression > "+" < Term  >
                    | < Expression > "-" < Term >
                    | < Term >

< Term >        ::= < Term  > "*"  < Factor >
                    | < Term > "/" < Factor >
                    | < Factor >

< Factor >      ::= < Base > "^" < Factor >
                    | < Base >

< Base >        ::= "(" < Expression > ")"
                    | "-" < Expression > 
                    | < Number >

< Number >      ::= < Integer-Lit > 
                    | < Real-Lit >
                    | < Ref >

< Ref >         ::= < Identifier >
                    | < Ref > "." < Ref >
                    | < Ref > "[" < Index > "]"

< Index >       ::= < Index > "," < Expression >
                    | < Expression >

< Integer-Lit > ::= < Integer-Lit > < Digit >
                    | < Digit >

< Real-Lit >    ::= < Integer-Lit > "." < Integer-Lit >

< Digit >       ::= "0"|"1"|"2"|"3"|"4"|"5"|"6"|"7"|"8"|"9"

< Identifier >  ::= < Identifier > < Letter >
                    | < Identifier > "_"
                    | < Identifier > < Digit >
                    | < Letter >
                    | "_"

< Letter >      ::= Any letter from A-Z                    
